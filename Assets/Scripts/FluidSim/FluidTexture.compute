#pragma kernel CSMain

RWTexture2D<float4> Result;      // output render texture
StructuredBuffer<float> U;
StructuredBuffer<float> V;
StructuredBuffer<float> Density;
StructuredBuffer<float> Pressure;
StructuredBuffer<float> Vorticity;
StructuredBuffer<int> Solid;


Texture2D<float4> VelocityLUT;
Texture2D<float4> PressureLUT;
Texture2D<float4> VorticityLUT;

SamplerState sampler_VelocityLUT;
SamplerState sampler_PressureLUT;
SamplerState sampler_VorticityLUT;

int Width;
int Height;
int textureWidth;
int textureHeight;
float CellSize;
float MaxVelocityMag;
float MaxDensity;
float MaxPressureMag;
float MaxVorticityMag;
float4 solidCellColor;
float4 backgroundColor;
uint visualizationMode; // 0: velocity, 1: pressure, 2: density, 3: velocity+density

float bilinearSample(StructuredBuffer<float> buf, float2 uv)
{
    int x0 = clamp(floor(uv.x), 0, Width - 1);
    int x1 = clamp(x0 + 1, 0, Width - 1);
    int y0 = clamp(floor(uv.y), 0, Height - 1);
    int y1 = clamp(y0 + 1, 0, Height - 1);

    float sx = uv.x - x0;
    float sy = uv.y - y0;

    float c00 = buf[x0 + y0 * Width];
    float c10 = buf[x1 + y0 * Width];
    float c01 = buf[x0 + y1 * Width];
    float c11 = buf[x1 + y1 * Width];

    float c0 = lerp(c00, c10, sx);
    float c1 = lerp(c01, c11, sx);

    return lerp(c0, c1, sy);
}

[numthreads(8,8,1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    float gridWidth = Width * CellSize;
    float gridHeight = Height * CellSize;
    
    if (id.x >= textureWidth || id.y >= textureHeight) return;

    // Map pixel to world coords
    float worldX = (id.x + 0.5) * gridWidth / textureWidth;
    float worldY = (id.y + 0.5) * gridHeight / textureHeight;

    // Convert to cell index
    float gridX = worldX / CellSize;
    float gridY = worldY / CellSize;

    int i = clamp(floor(gridX), 0, Width - 1);
    int j = clamp(floor(gridY), 0, Height - 1);

    float4 color = float4(0,0,0,1);
    
    if (Solid[i + j * Width] == 1)
    {
        color = solidCellColor; // solidCellColor
        Result[id.xy] = color;
        return;
    }
    
    if (visualizationMode == 0) // velocity
    {
        float u = bilinearSample(U, float2(gridX, gridY - 0.5));
        float v = bilinearSample(V, float2(gridX - 0.5, gridY));
        float velMag = length(float2(u,v));

        float2 uv = float2(saturate(velMag / MaxVelocityMag), 0.5);
        color = VelocityLUT.SampleLevel(sampler_VelocityLUT, uv, 0);
    }
    else if (visualizationMode == 1) // pressure
    {
        float pressure = bilinearSample(Pressure, float2(gridX - 0.5, gridY - 0.5));
        float pMag = abs(pressure);

        float2 uv = float2(saturate(pMag / MaxPressureMag), 0.5);
        color = PressureLUT.SampleLevel(sampler_PressureLUT, uv, 0);
    }
    else if (visualizationMode == 2) // density
    {
        float density = bilinearSample(Density, float2(gridX - 0.5, gridY - 0.5));
        float t = saturate(density / MaxDensity);
        color.rgb = lerp(backgroundColor, float3(1, 1, 1), t);
    }
    else if (visualizationMode == 3) // Vorticity
    {
        float vorticity = bilinearSample(Vorticity, float2(gridX - 0.5, gridY - 0.5));
        float vMag = abs(vorticity);
        float2 uv = float2(saturate(vMag / MaxVorticityMag), 0.5);
        color = VorticityLUT.SampleLevel(sampler_VorticityLUT, uv, 0);
    }
    else if (visualizationMode == 4) // pressure + density
    {
        float pressure = bilinearSample(Pressure, float2(gridX - 0.5, gridY - 0.5));
        float pMag = abs(pressure);

        float density = bilinearSample(Density, float2(gridX - 0.5, gridY - 0.5));
        float densityT = saturate(density / MaxDensity);

        float2 uv = float2(saturate(pMag / MaxPressureMag), 0.5);
        float3 pressureColor = PressureLUT.SampleLevel(sampler_PressureLUT, uv, 0).rgb;

        color.rgb = lerp(backgroundColor, pressureColor, densityT);
    }
    else if (visualizationMode == 5) // velocity + density
    {
        float u = bilinearSample(U, float2(gridX, gridY - 0.5));
        float v = bilinearSample(V, float2(gridX - 0.5, gridY));
        float velMag = length(float2(u,v));

        float density = bilinearSample(Density, float2(gridX - 0.5, gridY - 0.5));
        
        // simple example
        float2 uv = float2(saturate(velMag / MaxVelocityMag), 0.5);
        float densityT = saturate(density / MaxDensity);

        float3 velColor = VelocityLUT.SampleLevel(sampler_VelocityLUT, uv, 0).rgb;
        color.rgb = lerp(backgroundColor, velColor, densityT);
    }
    else if (visualizationMode == 999) // DEBUG
    {
        // Show cell grid as checkerboard
        int i = clamp(floor(gridX), 0, Width - 1);
        int j = clamp(floor(gridY), 0, Height - 1);
    
        float checker = fmod(i + j, 2.0);
        color = float4(checker, checker, checker, 1);
    
        // Highlight solid cells in red
        if (Solid[i + j * Width] == 1)
        {
            color = float4(1, 0, 0, 1); // RED for solid
        }
    }
    Result[id.xy] = color;
}

